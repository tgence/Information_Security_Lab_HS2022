theory IKEv1
begin

/* IKEv1 protocol:
   A -> B : aenc_B{A}, aenc_B{N_A}, g^x  (N_A fresh nonce, x fresh)
   B -> A : aenc_A{B}, aenc_A{N_B}, g^y, HASH_B  (y fresh)
   A -> B : HASH_A
where:
   HASH_R = prf(<skID, g^x, g^y, A, B>)
   HASH_I = prf(<skID, g^y, g^x, B, A>)
   skID = prf(<h(<N_A, N_B>), A, B>)

     */


/* ----------------- */
/* Equational theory */
/* ----------------- */

/* Load the built-in Diffie-Hellman and signature equational theories */
builtins: diffie-hellman, asymmetric-encryption, hashing

/*Define an abstract function prf with no additional algebraic properties
representing a "perfect" pseudo-random function. */
functions: prf/1


/* ------------------ */
/* Key infrastructure */
/* ------------------ */

rule genkey:
   [ Fr(~kA) ] 
   -->
   [ !Key($A, ~kA),
     !Pk($A, pk(~kA)),
     Out(pk(~kA)) ]



/* Question 7*/

/*----------------*/
/* Protocol rules */
/*----------------*/

/* Initiator role */
rule Init_1:
  let gxI = 'g'^~xI 
  in         
  [ 
    Fr(~xI),
    Fr(~Na),
    !Pk($B, pkB) 
  ]             
   
  -->

  [ 
    Init_1($A, $B, ~xI, gxI, ~Na),
    Out(<aenc($A, pkB), aenc(~Na, pkB), gxI>)
  ]  


/* Responder role */
rule Resp_1:
  let gyR = 'g'^~yR
      Na = adec(enc_Na, skB)
      skID = prf(< h(<Na, ~Nb>), $A, $B >)
      HASH_b = prf(<skID, gyR, gxI, $B, $A>)
      g_xy = gxI^~yR
      K_ab = prf(<skID, g_xy>)
  in
  [ 
    Fr(~yR),
    Fr(~Nb),
    !Key($B, skB),
    !Pk($A, pkA),
    In(<enc_A, enc_Na, gxI>)                
  ]               
  
  --[
      RunningR($B, $A, K_ab),
      Eq($A, adec(enc_A, skB))
  ]->

  [ 
    Resp_1($A, $B, ~yR, gyR, gxI, Na, ~Nb, skID, HASH_b), 
    Out(<aenc($B, pkA), aenc(~Nb, pkA), gyR, HASH_b>) 
  ]


/* Initiator role */
rule Init_2:
  let Nb = adec(enc_Nb, skA)
      skID = prf(< h(<~Na, Nb>), $A, $B >)
      HASH_a = prf(<skID, gxI, gyR, $A, $B>)
      g_xy = gyR^~xI
      K_ab = prf(<skID, g_xy>)
  in
  [ 
    Init_1($A, $B, ~xI, gxI, ~Na),
    !Key($A, skA),
    !Pk($B, pkB),
    In(<enc_B, enc_Nb, gyR, HASH_b>)
  ]               
   
  --[ 
      FinishedI($A),          
      SecretI($A, $B , K_ab),
      Eq($B, adec(enc_B, skA)),
      Eq(prf(<skID, gyR, gxI, $B, $B>), HASH_b),
      CommitI($A, $B, K_ab),
      RunningI($A, $B, K_ab)]->      

  [Out(HASH_a)]



/* Responder role */
rule Resp_2:
  let g_xy = gxI^~yR
      K_ab = prf(<skID, g_xy>)
  in
  [
   Resp_1($A, $B, ~yR, gyR, gxI, Na, ~Nb, skID, HASH_b),
   In(HASH_a)
  ]

  --[ 
      FinishedR($B),             
      SecretR($A, $B, K_ab),
      Eq(prf(<skID, gxI, gyR, $A, $B>), HASH_a),
      CommitR($B, $A, K_ab)
  ]->

  []

/* ---------- */
/* Key Reveal */
/* ---------- */
rule Publish_secretK:
[!Key(A, k)]

--[Compromised(A)]->

[Out(k)]




/* ---------- */
/* Restrictions */
/* ---------- */

restriction equality:
"All x y #i. Eq(x,y) @i ==> x = y"


/* ---------- */
/* Properties */
/* ---------- */

/* Executability checks */
lemma executableI:
exists-trace "Ex #i A. FinishedI(A) @ i & not (Ex #j B. Compromised(B)@j)"

lemma executableR:
exists-trace "Ex #i B. FinishedR(B) @ i & not (Ex #j B. Compromised(B)@j)"


/* Secrecy and agreement properties */

lemma key_secrecyI:
"All #i A B k.
 (SecretI(A, B, k) @ i &
  not ((Ex #l. Compromised(A) @ l) | (Ex #m. Compromised(B) @ m)))
  ==> not (Ex #j. K(k) @ j)"

lemma key_secrecyR:
"All #i A B k.
 (SecretR(A, B, k) @ i &
  not ((Ex #l. Compromised(A) @ l) | (Ex #m. Compromised(B) @ m)))
  ==> not (Ex #j. K(k) @ j)"


lemma agreementR:
"All #i A B k.
  (CommitR( B, A, k) @ i &
   not ((Ex #l. Compromised(A) @ l) | (Ex #m. Compromised(B) @ m)))
   ==> (Ex #j. RunningI(A, B, k) @ j & j < i)"

lemma agreementI:
"All #i A B k.
  (CommitI( A, B, k) @ i &
   not ((Ex #l. Compromised(A) @ l) | (Ex #m. Compromised(B) @ m)))
   ==> (Ex #j. RunningR(B, A, k) @ j & j < i)"



/* Perfect Forward Secrecy:  (Question 8)
Please uncomment the following two lemmas for this question.
*/

lemma secrecy_PFS_R:
 " All A B k #i. 
    SecretR(A, B, k) @i &
      not ((Ex #l. (Compromised(A) @ l & l<i )) | (Ex #m. (Compromised(B) @ m & m < i)) )  
    ==> not (Ex #j. K(k)@j )"


lemma secrecy_PFS_I:
 " All A B k #i. 
    SecretI(A, B, k) @i &
      not ((Ex #l. (Compromised(A) @ l & l<i )) | (Ex #m. (Compromised(B) @ m & m < i)) )  
    ==> not (Ex #j. K(k)@j )"



end



